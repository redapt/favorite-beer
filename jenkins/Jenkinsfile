def buildCompleted = false

def isMergeCommit(String commit) {
  isSimpleCommit = sh(returnStdout: true,
      script: "set +e; git rev-parse --verify -q $commit^2 > /dev/null; echo \$?").trim()
  return isSimpleCommit == "0"
}

def buildSuffix(String branch, String version) {
  if(branch == "develop"){
    if(version =~ /.*-dev/){
        return ""
    }
    return "-dev"
  }
  if(branch == "release"){
    if(version =~ /.*-rc/){
        return ""
    }
    return "-rc"
  }
  if("${branch}" =~ /hotfix.*/){
    if(version =~ /.*-hotfix/){
        return ""
    }
    return "-hotfix"
  }
  return ""
}

pipeline {

    parameters {
        string(name: 'REGISTRY', defaultValue: '771502366784.dkr.ecr.us-east-2.amazonaws.com', description: 'What ECR does jenkins have access to for non production? ex: 771502366784.dkr.ecr.us-east-2.amazonaws.com [Note: jenkins-kaniko should be within this registry at kaniko:latest]')

        string(name: 'IMAGE', defaultValue: 'favorite-beer', description: 'What should the built/deployed image be called? ex: favorite-beer')

        string(name: 'LATEST_TAG', defaultValue: 'latest', description: 'Do you want to override the latest tag? default: latest')

        string(name: 'GIT_USER_EMAIL', defaultValue: 'no-reply@redapt.com', description: 'What email should be used when writing to github? default: no-reply@redapt.com')

        string(name: 'GIT_USER_FULL_NAME', defaultValue: 'Jenkins', description: 'What is the name of the user, when writing to github? default: Jenkins')
    }

    agent {
        kubernetes {
            label 'kaniko'
            yaml """
apiVersion: v1
kind: Pod
metadata:
  name: kaniko
spec:
  serviceAccountName: jenkins-sa-agent
  containers:
  - name: jnlp
    image: 'docker.io/jenkins/inbound-agent:4.3-4-alpine'
    args: ['\$(JENKINS_SECRET)', '\$(JENKINS_NAME)']
  - name: kaniko
    image: ${params.REGISTRY}/kaniko:latest
    imagePullPolicy: Always
    command:
    - /busybox/cat
    tty: true
  - name: tools
    image: argoproj/argo-cd-ci-builder:v1.0.0
    command:
    - cat
    tty: true
  restartPolicy: Never
"""
        }
    }
    stages {

        stage('CICD') {
            when {
                anyOf {
                    expression {
                        isMergeCommit(env.GIT_COMMIT)
                    }
                    not {
                        changelog '\\[skip ci\\]'
                    }
                }
            }
            environment {
                DOCKERFILE        = "Dockerfile"
                CONTEXT           = "./spa-react-netcore-redis/voting"
                TAG               = sh(script: 'git rev-parse HEAD', returnStdout: true).trim()
                REGISTRY          = "${params.REGISTRY}"
                IMAGE             = "${params.IMAGE}"
                VERSION           = sh(script: 'cat VERSION', returnStdout: true).trim()
                LATEST_TAG        = "${params.LATEST_TAG}"
                VERSION_BRANCH    = sh(script: 'echo ${CHANGE_BRANCH:=$BRANCH_NAME}', returnStdout: true).trim()
                VERSION_SUFFIX    = buildSuffix("${VERSION_BRANCH}", "${VERSION}")
                VERSION_CANDIDATE = "${VERSION}${VERSION_SUFFIX}"
                VERSION_BUILD     = "${VERSION_CANDIDATE}-${BUILD_NUMBER}"
            }
            stages {
                stage('Build Image') {
                    when {
                        anyOf {
                            allOf { 
                                changeRequest target: 'master'
                                changeRequest branch: 'hotfix/.*', comparator: 'REGEXP'
                            }
                            allOf { 
                                not {
                                    changeRequest()
                                }
                                branch 'release'
                            }
                            allOf { 
                                not {
                                    changeRequest()
                                }
                                branch 'develop'
                            }
                        }
                    }
                    steps {
                        //container('tools'){
                        //    sh "wget https://github.com/mikefarah/yq/releases/download/v4.9.6/yq_linux_amd64.tar.gz"
                        //    sh "tar xvf yq_linux_amd64.tar.gz"
                        //    sh "mv yq_linux_amd64 /usr/bin/yq"
                        
                        //    sh "chmod +x ./jenkins/scripts/bump_release_version.sh"
                        //    sh "./jenkins/scripts/bump_release_version.sh"
                        
                        //}
                        container(name: 'kaniko', shell: '/busybox/sh') {   
                            script {
                                try {
                                    sh '''#!/busybox/sh
                                    /kaniko/executor \
                                    --context=dir://${CONTEXT} \
                                    --dockerfile=${DOCKERFILE} \
                                    --destination=${REGISTRY}/${IMAGE}:${TAG} \
                                    --destination=${REGISTRY}/${IMAGE}:${VERSION_CANDIDATE} \
                                    --destination=${REGISTRY}/${IMAGE}:${VERSION_BUILD} \
                                    --destination=${REGISTRY}/${IMAGE}:${LATEST_TAG}
                                    '''
                                    buildCompleted = true
                                } catch (Exception e) {
                                    buildCompleted = false
                                }
                            }
                        }
                    }
                }

                stage('Deploy: Dev') {
                    when {
                        allOf { 
                            not {
                                changeRequest()
                            }
                            branch 'develop'
                        }
                    }
                    steps {
                        container('tools'){           
                            script {     
                                if(buildCompleted){
                                    echo "Deploy ${REGISTRY}/${IMAGE}:${VERSION_BUILD} to Dev Environment"
                                }
                            }
                        }
                    }
                }

                stage('Deploy: Test') {
                    when {
                        anyOf {
                            allOf { 
                                not {
                                    changeRequest()
                                }
                                branch 'release'
                            }
                        }
                    }
                    steps {
                        container('tools'){               
                            script {     
                                if(buildCompleted){
                                    echo "Deploy ${REGISTRY}/${IMAGE}:${VERSION_BUILD} to Test Environment"
                                }
                            }
                        }
                    }
                }

                stage('VERSION Commit') {
                    when {
                        anyOf {
                            allOf { 
                                not {
                                    changeRequest()
                                }
                                branch 'release'
                            }
                            allOf { 
                                changeRequest target: 'master'
                                anyOf {
                                   changeRequest branch: 'hotfix/.*', comparator: 'REGEXP'
                                }
                            }
                        }
                    }
                    steps {

                        container('tools'){               
                            echo "Tag Source Branch ${VERSION_BRANCH} and Set Version: ${VERSION_CANDIDATE} [skip ci]"
                            script {
                                if("${VERSION_CANDIDATE}" != "${VERSION}"){
                                    withCredentials([usernamePassword(credentialsId: 'jenkins-gh-user', usernameVariable: 'USERNAME', passwordVariable: 'PAT')]) {
                                        sh "git config --global user.email \"${params.GIT_USER_EMAIL}\""
                                        sh "git config --global user.name \"${params.GIT_USER_FULL_NAME}\""
                                        sh "git fetch origin release"
                                        sh "git checkout ${VERSION_BRANCH}"
                                        sh "echo ${VERSION_CANDIDATE} > VERSION"
                                        sh "git add VERSION"
                                        sh "git commit -m \"[skip ci] Bump version to ${VERSION_CANDIDATE}\""
                                        sh "git remote set-url origin https://$PAT@github.com/redapt/favorite-beer.git"
                                        sh "git push origin ${VERSION_BRANCH}"
                                    }
                                }
                            }
                        }
                    }
                }  

                // Have an awkward state when release PR to master is updated, need to reconcile the version on Release, 
                // BEFORE taking action on the PR. If/when I skip ci the version set after the build, the pull request would already have 
                // been triggered by the additional commit to an open PR.. (May need a rule to close PR to master, if it needs more code.)
                stage('Deploy: Stage') {
                    when {
                        anyOf {
                            allOf { 
                                changeRequest target: 'master'
                                anyOf {
                                   changeRequest branch: 'hotfix/.*', comparator: 'REGEXP'
                                   changeRequest branch: 'release', comparator: 'REGEXP'
                                }
                            }
                        }
                    }
                    steps {
                        container('tools'){             
                            script {     
                                if(buildCompleted){
                                    echo "Deploy ${REGISTRY}/${IMAGE}:${VERSION_CANDIDATE} as ${REGISTRY}/${IMAGE}:${VERSION_CANDIDATE}-${BRANCH_NAME}-${BUILD_NUMBER} to Stage Environment"
                                } else if(env.CHANGE_BRANCH == 'release') {
                                    echo "AFTER BUILD if PR was already OPEN, Deploy ${REGISTRY}/${IMAGE}:${VERSION_CANDIDATE} as ${REGISTRY}/${IMAGE}:${VERSION_CANDIDATE}-${BRANCH_NAME}-${BUILD_NUMBER} to Stage Environment"
                                }
                            }
                        }
                    }
                }

                stage('Deploy: Prod') {
                    when {
                        allOf { 
                            not {
                                changeRequest()
                            }
                            branch 'master'
                        }
                    }
                    environment {
                        VERSION_CANDIDATE = sh(script: 'cat VERSION', returnStdout: true).trim()
                        VERSION           = env.VERSION_CANDIDATE.substring(0, env.VERSION_CANDIDATE.indexOf('-')) // This fails, if the suffix is not on the VERSION file, to determine Hotfix or Release
                    }
                    steps {
                        container('tools'){      
                            echo "Promote ${REGISTRY}/${IMAGE}:${VERSION_CANDIDATE} as ${IMAGE}:${VERSION} to Production Registry"           
                            echo "Deploy ${IMAGE}:${VERSION} to Prod Environment"
                            echo "Down-Merge Master into Release, THEN bump Release version no suffix [skip ci]"
                        }
                    }
                }
            }
        }

    }
}